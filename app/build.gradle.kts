@file:Suppress("UnstableApiUsage")

plugins {
    alias(libs.plugins.sefirah.android.application)
    alias(libs.plugins.compose.compiler)
}

val appName = providers.gradleProperty("sefirah.appName")
    .orElse(providers.gradleProperty("aikyam.appName"))
    .getOrElse("Sefirah AI")
val appId = providers.gradleProperty("sefirah.applicationId")
    .orElse(providers.gradleProperty("aikyam.applicationId"))
    .getOrElse("com.castle.sefirah.ai")
val verName = providers.gradleProperty("sefirah.versionName")
    .orElse(providers.gradleProperty("aikyam.versionName"))
    .getOrElse("3.1.0")
val verCode = providers.gradleProperty("sefirah.versionCode")
    .orElse(providers.gradleProperty("aikyam.versionCode"))
    .getOrElse("35").toInt()

android {
    namespace = "com.castle.sefirah"

    buildFeatures {
        buildConfig = true
    }


    defaultConfig {
        applicationId = appId

        versionCode = verCode
        versionName = verName
        testInstrumentationRunner = "androidx.test.runner.AndroidJUnitRunner"
        buildConfigField("String", "APP_NAME", "\"$appName\"")

        externalNativeBuild {
            cmake {
                arguments += listOf("-DANDROID_STL=none")
            }
        }
    }

    dependenciesInfo {
        includeInApk = false
        includeInBundle = false
    }

    buildTypes {
        release {
            isMinifyEnabled = true
            isShrinkResources = true
            proguardFiles(getDefaultProguardFile("proguard-android-optimize.txt"), "proguard-rules.pro")
        }
    }

    buildFeatures {
        compose = true
    }

    packaging {
        resources {
            excludes += "/META-INF/{AL2.0,LGPL2.1,DEPENDENCIES}"
            excludes += "META-INF/versions/**"
        }
        jniLibs {
            useLegacyPackaging = true
        }
    }

    externalNativeBuild {
        cmake {
            path = file("src/main/jni/CMakeLists.txt")
        }
    }
}

kotlin {
    compilerOptions {
        freeCompilerArgs.addAll(
            "-opt-in=androidx.compose.animation.ExperimentalAnimationApi",
            "-opt-in=androidx.compose.animation.graphics.ExperimentalAnimationGraphicsApi",
            "-opt-in=androidx.compose.foundation.ExperimentalFoundationApi",
            "-opt-in=androidx.compose.foundation.layout.ExperimentalLayoutApi",
            "-opt-in=androidx.compose.material3.ExperimentalMaterial3Api",
            "-opt-in=androidx.compose.ui.ExperimentalComposeUiApi",
            "-opt-in=kotlinx.coroutines.FlowPreview",
        )
    }
}

dependencies {
    api(projects.core.common)
    api(projects.core.network)
    api(projects.core.presentation)
    api(projects.data)
    api(projects.domain)

    implementation(libs.androidx.appcompat)
    implementation(libs.material)
    implementation(libs.hilt.navigation.compose)
    implementation(libs.core.ktx)
    implementation(libs.androidx.work)
    implementation(libs.lifecycle.runtime.ktx)
    implementation(platform(libs.compose.bom))
    implementation(libs.bundles.compose.android)
    implementation(libs.material3.adaptive.navigation.suite)
    implementation(libs.coil.compose)
    implementation(libs.compose.navigation)
    implementation(libs.splashscreen)

    implementation(libs.richtext.m3)
    implementation(libs.richtext.commonmark)
    implementation(libs.reorderable)

    implementation(libs.androidx.media)
    implementation(libs.androidx.hilt.work)
    implementation(libs.zxing.cpp.android)
    implementation(libs.camerax.core)
    implementation(libs.camerax.camera2)
    implementation(libs.camerax.lifecycle)
    implementation(libs.camerax.view)
    implementation(libs.guava)

    compileOnly(libs.hidden.stub)

    testImplementation(libs.junit)
    androidTestImplementation(libs.androidx.junit)
    androidTestImplementation(libs.androidx.espresso.core)
    androidTestImplementation(platform(libs.compose.bom))
    androidTestImplementation(libs.compose.ui.test.junit)
    debugImplementation(libs.compose.ui.tooling)
    debugImplementation(libs.compose.ui.test.manifest)
}